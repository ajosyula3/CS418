<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>MP2: Terrain Modeling</title>
<meta charset="utf-8"> 
</head>

    
<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexNormal;
    attribute vec3 aVertexPosition;
   
    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec3 vNormal;
    varying vec3 vPosition;

    uniform float uMinHeight;
    uniform float uMaxHeight;

    varying vec3 vKDiffuse;

    void main(void) {
        // Get the vertex position in eye coordinates
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        vPosition = (uMVMatrix * vec4(aVertexPosition, 1.0)).xyz;
        // Calculate the normal
        vNormal = uNMatrix * aVertexNormal;

        // Normalize height to value between 0 to 1
        float height = (aVertexPosition.z - uMinHeight) / (uMaxHeight - uMinHeight);
        if (height < 0.25) {
            vKDiffuse = vec3(0.0/255.0, 0.0/255.0, 255.0/255.0); // blue
        } else if (height < 0.50) {
            vKDiffuse = vec3(0.0/255.0, 128.0/255.0, 0.0/255.0); // green
        } else if (height < 0.75) {
            vKDiffuse = vec3(165.0/255.0, 42.0/255.0, 42.0/255.0); // brown
        } else {
            vKDiffuse = vec3(255.0/255.0, 255.0/255.0, 255.0/255.0); // white
        }
    }
</script>
  
<script id="shader-fs" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPosition;
    uniform vec3 uAmbientLightColor;
    uniform vec3 uDiffuseLightColor;
    uniform vec3 uSpecularLightColor;

    uniform vec3 uKAmbient;
    uniform vec3 uKDiffuse;
    uniform vec3 uKSpecular;
    uniform float uShininess;

    varying vec3 vNormal;
    varying vec3 vPosition;

    varying vec3 vKDiffuse;

    uniform bool uFog;

    void main(void) {
        float fogCoord = (gl_FragCoord.z/gl_FragCoord.w);
        vec4 fogColor = vec4(1.0, 1.0, 1.0, 1.0);

        const float LOG2 = 1.442695;
        float fogDensity = 0.6;
        float fogFactor = exp2(-fogDensity * fogDensity * fogCoord * fogCoord * LOG2);
        if (uFog) {
            fogFactor = clamp(fogFactor, 0.0, 1.0);
        } else {
            fogFactor = 1.0;
        }

        // Calculate the vector (l) to the light source
        vec3 vectorToLightSource = normalize(uLightPosition - vPosition);
        
        // Transform the normal (n) to eye coordinates
        vec3 normalEye = normalize(vNormal);
        
        // Calculate n dot l for diffuse lighting
        float diffuseLightWeightning = max(dot(normalEye, 
                                        vectorToLightSource), 0.0);
        
        // The camera in eye coordinates is located in the origin and is pointing
        // along the negative z-axis. Calculate viewVector (v) 
        // in eye coordinates as:
        // (0.0, 0.0, 0.0) - vPosition
        vec3 viewVectorEye = -normalize(vPosition);
        
        // Blinn-Phong reflection model
        vec3 halfwayVector = normalize(vectorToLightSource + viewVectorEye);

        float ndoth = max(dot(halfwayVector, normalEye), 0.0);
        
        float specularLightWeightning = pow(ndoth, uShininess);

        // Sum up all three reflection components and send to the fragment shader
        vec4 fragColor = vec4(((uAmbientLightColor*uKAmbient)
                    + (uDiffuseLightColor*vKDiffuse) * diffuseLightWeightning
                    + ((uSpecularLightColor*uKSpecular) * specularLightWeightning)),1.0);

        gl_FragColor = mix(fogColor, fragColor, fogFactor);
    }
</script>

<script src="gl-matrix-min.js"></script> 
<script src="webgl-utils.js"></script> 
<script src="Terrain.js"></script>  
<script src="mp2.js"></script>   
<body onload="startup();">
 <canvas id="myGLCanvas" width="800" height="800"></canvas>
    <form id="input_form">
        <fieldset>
        <legend>Rendering Parameters</legend>
        <input type="radio" name="primitive" id="wireframe" value="wireframe"> Wireframe
        <input type="radio" name="primitive" id="polygon" id="polygon" value="polygon" checked> Polygon
        <input type="radio" name="primitive" id="wirepoly" value="wirepoly" > Polygon with Edges  
        </fieldset>

        <fieldset>
            <legend>Fog Effect</legend>
            <input type="radio" name="fog" id="fog" checked> On
            <input type="radio" name="fog"> off
        </fieldset>

        <fieldset>
            <legend>Dashboard</legend>
            <emph>Roll Degree</emph> <input type="number" id="roll"> Keys Left and Right <br>
            <emph>Pitch Degree</emph> <input type="number" id="pitch"> Keys Up and Down <br>
            <emph>Speed</emph> <input type="number" id="speed"> Keys + and -
        </fieldset>
    </form>
</body>

</html>
