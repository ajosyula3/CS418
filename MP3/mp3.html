<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>Hello Mesh </title>
<meta charset="utf-8"> 
</head>

    
<script id="shader-vs" type="x-shader/x-vertex">
attribute vec3 aVertexNormal;
attribute vec3 aVertexPosition;

uniform mat4 uMVMatrix;
uniform mat4 uVMatrix;
uniform mat4 uMMatrix;
uniform mat4 uPMatrix;
uniform mat3 uNMatrix;

uniform vec3 uEye;
uniform int uTextureType;
uniform vec3 uLightPosition;

varying vec3 vDirection;
varying vec3 vPosition;
varying vec3 vNormal;
varying vec3 vLight;

void main(void) {
  // Get the vertex position in eye coordinates
  vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
  vPosition = vertexPositionEye4.xyz / vertexPositionEye4.w;
  
  // Transform the normal (n) to eye coordinates
  vNormal = normalize(uNMatrix * aVertexNormal);

  vLight = (uVMatrix * vec4(uLightPosition, 1.0)).xyz;
              
  gl_Position = uPMatrix*uMVMatrix*vec4(aVertexPosition, 1.0);

  vec3 positionWorld = (uMMatrix * vec4(aVertexPosition, 1.0)).xyz;
  vec3 normalWorld = normalize(mat3(uMMatrix) * aVertexNormal);
  vec3 eyeToVertex = normalize(positionWorld - uEye);
  if (uTextureType == 0 || uTextureType == 1) {
    vDirection = reflect(eyeToVertex, normalWorld);
  } else if (uTextureType == 2) {
    vDirection = refract(eyeToVertex, normalWorld,  1.00/1.52);
  }
}
</script>
  
<script id="shader-fs" type="x-shader/x-fragment">
precision mediump float;

uniform vec3 uAmbientLightColor;
uniform vec3 uDiffuseLightColor;
uniform vec3 uSpecularLightColor;

uniform vec3 uKAmbient;
uniform vec3 uKDiffuse;
uniform vec3 uKSpecular;
uniform float uShininess;

uniform samplerCube uTexture;
uniform bool uPhong;

varying vec3 vPosition;
varying vec3 vNormal;
varying vec3 vDirection;
varying vec3 vLight;

void main(void) {
  // Calculate the vector (l) to the light source
  vec3 lightEye = vLight;
  vec3 vectorToLightSource = normalize(lightEye - vPosition);

  // Calculate n dot l for diffuse lighting
  float diffuseLightWeightning = max(dot(vNormal, 
                                  vectorToLightSource), 0.0);
                                     
  // Calculate the reflection vector (r) that is needed for specular light
  vec3 reflectionVector = normalize(reflect(-vectorToLightSource, 
                                           vNormal));
  
  // The camera in eye coordinates is located in the origin and is pointing
  // along the negative z-axis. Calculate viewVector (v) 
  // in eye coordinates as:
  // (0.0, 0.0, 0.0) - vPosition
  vec3 viewVectorEye = -normalize(vPosition);

  float rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);
    
  float specularLightWeightning = pow(rdotv, uShininess);

  // Sum up all three reflection components and send to the fragment shader
  vec4 vColor = vec4(((uAmbientLightColor*uKAmbient)
              + (uDiffuseLightColor*uKDiffuse) * diffuseLightWeightning
              + ((uSpecularLightColor*uKSpecular) * specularLightWeightning)),1.0);

  if (uPhong) {
    gl_FragColor = vColor;
  } else {
    gl_FragColor = textureCube(uTexture, vDirection);
  }
}
</script>

<script id="skybox-vs" type="x-shader/x-vertex">
attribute vec3 aVertexNormal;
attribute vec3 aVertexPosition;

uniform mat4 uVMatrix;
uniform mat4 uPMatrix;

varying vec3 vPosition;

void main(void) {
  gl_Position = uPMatrix*uVMatrix*vec4(aVertexPosition, 1.0);
  vPosition = aVertexPosition;
}
</script>

<script id="skybox-fs" type="x-shader/x-fragment">
precision mediump float;
uniform samplerCube uSkybox;

varying vec3 vPosition;

void main(void) {
  gl_FragColor = textureCube(uSkybox, vPosition);
  // gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
}
</script>

<script src="gl-matrix-min.js"></script> 
<script src="webgl-utils.js"></script> 
<script src="TriMesh.js"></script>  
<script src="mp3.js"></script>
<body onload="startup();">
 <canvas id="myGLCanvas" width="800" height="800"></canvas>
    <form id="input_form">
        <fieldset>
            <legend>Rendering Parameters</legend>
         <input type="radio" name="primitive" id="wireframe" value="wireframe"> Wireframe
         <input type="radio" name="primitive" id="polygon" id="polygon" value="polygon" checked> Polygon
         <input type="radio" name="primitive" id="wirepoly" value="wirepoly" > Polygon with Edges  
        </fieldset>
        <fieldset>
          <legend>Teapot Texture</legend>
       <input type="radio" name="texture" id="phong" value="phong" checked> Phong Shading
       <input type="radio" name="texture" id="reflective" value="reflective"> Reflective
       <input type="radio" name="texture" id="refractive" value="refractive"> Refractive
      </fieldset>
        <fieldset>
            <legend>Mesh Pose and Camera Position</legend>
         <emph>Euler Y:</emph> <input type="number" id="eY" value="0"> Keys A and D <br><br>
         <emph>Eye Z position:</emph> <input type="number" id="eZ" value="0" > Keys ArrowUp and ArrowDown   
        </fieldset>
        </form>
</body>

</html>
